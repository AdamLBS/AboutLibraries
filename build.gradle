buildscript {
    apply from: 'configurations.gradle'

    ext {
        release = [
                versionName: "$VERSION",
                versionCode: Integer.parseInt(rootProject.properties["VERSION_CODE"])
        ]

        setup = [
                compileSdk: 30,
                buildTools: "30.0.3",
                minSdk    : 16,
                targetSdk : 30
        ]

        versions = [
                androidX        : '1.0.0',
                cardview        : '1.0.0',
                appcompat       : '1.3.0',
                recyclerview    : '1.2.0',
                material        : '1.3.0',
                kotlin          : "1.5.10",
                constraintLayout: '2.0.4',
                navigation      : "2.3.5",
                iconics         : "5.2.8",
                detekt          : '1.17.1',
                fastadapter     : "5.4.1",
                materialdrawer  : "8.4.0",
                coreKtx         : "1.5.0"
        ]
    }

    repositories {
        gradlePluginPortal()
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:${versions.navigation}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detekt}"
        classpath "com.mikepenz.aboutlibraries.plugin:aboutlibraries-plugin"
    }
}

allprojects {
    group "com.mikepenz"

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven {
            // (mp) remove when available on maven center
            url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven"
            content { includeGroup "org.jetbrains.kotlinx" }
        }
    }

    configurations.all {
        resolutionStrategy.force "org.jetbrains.kotlinx:kotlinx-html-jvm:0.7.3"
        // (mp) remove when available on maven center
    }
}

subprojects {
    apply from: "$rootDir/detekt.gradle"

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${versions.detekt}"
    }
}
